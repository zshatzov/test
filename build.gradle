buildscript {
	ext {
		springBootVersion = '1.4.3.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'groovy'
apply plugin: 'eclipse-wtp'
apply plugin: 'org.springframework.boot'
apply plugin: 'war'

war {
	baseName = 'airportInfo'
	version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

configurations {
	jaxb
	providedRuntime
}

dependencies {
	compile 'org.springframework.ws:spring-ws-core'
	compile('org.springframework.boot:spring-boot-starter-jersey'){
		exclude group: 'ch.qos.logback'
	}
	compile group: 'org.codehaus.groovy', name: 'groovy', version: '2.4.8'
	compile group: 'org.codehaus.groovy', name: 'groovy-xml', version: '2.4.8'
	runtime group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.22'
	jaxb "com.sun.xml.bind:jaxb-xjc:2.1.7"
	providedRuntime('org.springframework.boot:spring-boot-starter-tomcat')
	testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4-rc-3'
	testCompile 'org.spockframework:spock-spring:1.0-groovy-2.4'
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testRuntime group: 'cglib', name: 'cglib-nodep', version: '3.2.4'
}

task genAirportInfoModel {
	ext.sourcesDir = 'src/main/java'
	ext.classesDir = "${buildDir}/classes/jaxb"
	ext.schema = "http://www.webservicex.net/airport.asmx?WSDL"

	outputs.dir classesDir

	doLast() {
		project.ant {
			taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
					classpath: configurations.jaxb.asPath

			xjc(destdir: sourcesDir, schema: schema, package: "org.zenovy.amex.model.airport") {
				arg(value: "-wsdl")
				produces(dir: sourcesDir, includes: "**/*.java")
			}

		}
	}
}


task genServiceDetailModel {
	ext.sourcesDir = 'src/main/java'
	ext.classesDir = "${buildDir}/classes/jaxb"
	ext.schema = "http://www.webservicex.net/country.asmx?WSDL"

	outputs.dir classesDir

	doLast() {
		project.ant {
			taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
					classpath: configurations.jaxb.asPath

			xjc(destdir: sourcesDir, schema: schema, package: "org.zenovy.amex.model.details") {
				arg(value: "-wsdl")
				produces(dir: sourcesDir, includes: "**/*.java")
			}

		}
	}
}
